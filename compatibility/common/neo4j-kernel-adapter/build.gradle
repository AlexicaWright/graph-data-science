apply plugin: 'java-library'

description = 'Neo4j Graph Data Science :: Neo4j Kernel Adapter'

evaluationDependsOn(':neo4j-kernel-adapter-4.3')

dependencies {
    annotationProcessor group: 'org.immutables', name: 'builder',            version: ver.'immutables'
    annotationProcessor group: 'org.immutables', name: 'value',              version: ver.'immutables'

    compileOnly project(':annotations')
    compileOnly         group: 'org.immutables', name: 'builder',            version: ver.'immutables'
    compileOnly         group: 'org.immutables', name: 'value-annotations',  version: ver.'immutables'
    compileOnly         group: 'org.jetbrains',  name: 'annotations',        version: ver.'jetbrains-annotations'
    neodeps.each {
        compileOnly(group: 'org.neo4j', name: it, version: ver.'neo4j') {
            transitive = false
        }
    }

    api project(':neo4j-adapter')
    api project(':neo4j-kernel-adapter-api')

    compatibilityModules.'kernel-adapter'.each { dep ->
        if (dep.name.contains('4.3')) {
            implementation files(dep.sourceSets.drop2.output.files)
            implementation files(dep.sourceSets.head.output.files)
        } else {
            implementation dep
        }
    }

    testImplementation project(":test-utils")
}

