ext {
    releasedNeos = [
        '4.1'           : properties.getOrDefault('neo4jVersion41', '4.1.11'),
        '4.2'           : properties.getOrDefault('neo4jVersion42', '4.2.12'),
        '4.3'           : properties.getOrDefault('neo4jVersion43', '4.3.7'),
        '4.4'           : properties.getOrDefault('neo4jVersion44', '4.4.0')
    ]

    neos = releasedNeos +
    [
        '4.3.3'         : properties.getOrDefault('neo4jVersion433', '4.3.3'),
        '4.3.4plus'     : properties.getOrDefault('neo4jVersion434', '4.3.7'),
        '4.3.0-drop05.0': properties.getOrDefault('neo4jVersion43drop5', '4.3.0-drop05.0'),
        'dev'           : properties.getOrDefault('neo4jVersionDev', '5.0.0-dev'),
    ]

    neo4j_minor = properties.getOrDefault('neo4jVersion', neos.'4.3').toString().split('\\.')[0..1].join('.')

    neo_at_least_43 = [
        '4.1': neos.'4.3',
        '4.2': neos.'4.3',
        '4.3': neos.'4.3',
        '4.4': neos.'4.4',
        '5.0': neos.'dev',
    ]
    scalas = [
        '4.0': '2.12.7',
        '4.1': '2.12.10',
        '4.2': '2.12.10',
        '4.3': '2.12.13',
        '4.4': '2.12.13',
        '5.0': '2.12.13',
    ]
    ver = [
        doc                    : [
            'asciidoctorj'        : '2.4.3',
            'asciidoctorjPdf'     : '1.5.4',
            'asciidoctorjDiagram' : '2.1.0',
            'asciidoctorGroovyDsl': '1.0.0.Alpha2',
            'dom4j'               : '2.1.1',
            'jaxen'               : '1.1.6',
            'reflections'         : '0.9.11'
        ],
        'assertj'              : '3.20.2',
        'auto-common'          : '1.1.2',
        'auto-service'         : '1.0',
        'bouncycastle'         : '1.68',
        'commons-compress'     : '1.19',
        'commons-io'           : '2.11.0',
        'commons-lang3'        : '3.11',
        'commons-math3'        : '3.6.1',
        'commons-text'         : '1.9',
        'compile-testing'      : '0.19',
        'cypher-dsl'           : '2021.2.0',
        'deeplearning4j'       : '1.0.0-beta7',
        'eclipse-collections'  : '10.4.0',
        'ejml'                 : '0.40',
        'fastcsv'              : '1.0.4',
        'gdl'                  : '0.3.7',
        'hamcrest'             : '2.1',
        'HdrHistogram'         : '2.1.9',
        'hppc'                 : '0.7.3',
        'immutables'           : '2.8.8',
        'jackson'              : '2.13.0',
        'javapoet'             : '1.11.1',
        'jetbrains-annotations': '18.0.0',
        'jjwt'                 : '0.11.0',
        'jol'                  : '0.16',
        'jqwik'                : '1.6.1',
        'junit4'               : '4.13.2',
        'junit5'               : '5.8.1',
        'lucene'               : '8.8.0',
        'memoryfilesystem'     : '2.2.0',
        'mockito'              : '3.11.2',
        'neo4j'                : properties.getOrDefault('neo4jVersion', neos.'4.3'),
        'neo4j_at_least_43'    : neo_at_least_43[neo4j_minor],
        'opencypher-front-end' : '9.0.20210312',
        'picocli'              : '4.6.1',
        'qala-datagen'         : '2.3.0',
        'RoaringBitmap'        : '0.9.16',
        'sb-contrib'           : '7.4.7',
        'scala'                : scalas[neo4j_minor],
        'spotbugsToolVersion'  : '4.2.3',
        'protobuf'             : '3.19.0',
        'kryo'                 : '5.0.4',
    ]
    dep = [
        junit5bom:          "org.junit:junit-bom:${ver.junit5}",
        junit5jupiter:      "org.junit.jupiter:junit-jupiter",
        junit5platform:     "org.junit.platform:junit-platform-launcher",
        junit5migration:    "org.junit.jupiter:junit-jupiter-migrationsupport",
    ]
}

def neo_deps = [
    'annotations',
    'neo4j-collections',
    'neo4j-command-line',
    'neo4j-common',
    'neo4j-configuration',
    'neo4j-graphdb-api',
    'neo4j-id-generator',
    'neo4j-import-util',
    'neo4j-index',
    'neo4j-io',
    'neo4j-kernel-api',
    'neo4j-kernel',
    'neo4j-layout',
    'neo4j-logging',
    'neo4j-monitoring',
    'neo4j-procedure-api',
    'neo4j-record-storage-engine',
    'neo4j-resource',
    'neo4j-schema',
    'neo4j-storage-engine-api',
    'neo4j-token-api',
    'neo4j-values',
    'neo4j-lock',
    'neo4j-diagnostics',
    'neo4j-concurrent',
    'neo4j',
]

def neo_deps_dev = neo_deps - ['neo4j-storage-engine-api']

ext.neodeps = {
    if (ver.'neo4j' == neos.'dev') {
        neo_deps_dev.iterator()
    } else {
        neo_deps.iterator()
    }
}
