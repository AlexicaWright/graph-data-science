apply plugin: 'java-library'

description = 'Neo4j Graph Data Science :: Core'

dependencies {
    annotationProcessor project(':annotations')
    annotationProcessor project(':config-generator')
    annotationProcessor group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    annotationProcessor group: 'org.immutables',           name: 'value',              version: ver.'immutables'
    annotationProcessor group: 'org.neo4j',                name: 'annotations',        version: ver.'neo4j'

    compileOnly         group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    compileOnly         group: 'org.immutables',           name: 'value-annotations',  version: ver.'immutables'
    compileOnly         group: 'org.jetbrains',            name: 'annotations',        version: ver.'jetbrains-annotations'

    neodeps.each {
        compileOnly(group: 'org.neo4j', name: it, version: ver.'neo4j') {
            transitive = false
        }
    }
    compileOnly group: 'org.scala-lang', name: 'scala-library', version: ver.'scala'

    implementation project(':annotations')
    implementation project(':memory-usage')
    implementation project(':string-formatting')

    api(
            project(':neo4j-kernel-adapter'),
            project(':graph-projection-api'),
            [group: 'org.bouncycastle',        name: 'bcpkix-jdk15on',           version: ver.'bouncycastle'],
            [group: 'org.apache.commons',      name: 'commons-text',             version: ver.'commons-text'],
            [group: 'org.opencypher',          name: 'cypher-javacc-parser-9.0', version: ver.'opencypher-front-end', transitive: false],
            [group: 'org.opencypher',          name: 'cypher-ast-factory-9.0',   version: ver.'opencypher-front-end', transitive: false],
            [group: 'org.eclipse.collections', name: 'eclipse-collections-api',  version: ver.'eclipse-collections'],
            [group: 'org.eclipse.collections', name: 'eclipse-collections',      version: ver.'eclipse-collections'],
            [group: 'com.carrotsearch',        name: 'hppc',                     version: ver.'hppc'],
            [group: 'org.openjdk.jol',         name: 'jol-core',                 version: ver.'jol'],
            [group: 'org.hdrhistogram',        name: 'HdrHistogram',             version: ver.'HdrHistogram'],
            [group: 'org.jctools',             name: 'jctools-core',             version: ver.'jctools-core'],
            [group: 'io.jsonwebtoken',         name: 'jjwt-api',                 version: ver.'jjwt'],
    )

    implementation(
        [group: 'io.jsonwebtoken', name: 'jjwt-impl', version: ver.'jjwt'],
        [group: 'org.roaringbitmap', name: 'RoaringBitmap', version: ver.'RoaringBitmap'],
    )

    // Set hardcoded version of jackson-databind until there is a new version of jjwt-jackson
    implementation(group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: ver.'jjwt') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.6'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: ver['jackson']

    // TEST DEPENDENCIES

    testCompileOnly group: 'org.immutables', name: 'builder', version: ver.'immutables'

    testAnnotationProcessor project(':annotations')
    testAnnotationProcessor group: 'org.immutables', name: 'value', version: ver.'immutables'

    testImplementation project(':test-utils')

    testImplementation group: 'net.jqwik',       name: 'jqwik',               version: ver.'jqwik'
    testImplementation group: 'io.qala.datagen', name: 'qala-datagen-junit5', version: ver.'qala-datagen'
}
