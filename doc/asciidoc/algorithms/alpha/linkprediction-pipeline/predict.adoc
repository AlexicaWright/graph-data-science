[[algorithms-link-prediction-pipelines-predict]]
== Applying a trained model for prediction

In the previous sections we have seen how to build up a Link Prediction training pipeline and run it produce a runnable model.
After <<algorithms-ml-linkprediction-pipelines-train, training>>, the runnable model is of type `Link prediction pipeline` and resides in the model catalog.

The model can then be applied to a graph in the graph catalog to create a new relationship type containing the predicted links.
The relationships also have a property which stores the predicted probability of the link, which can be seen as a relative measure of the model's prediction confidence.

Since the model has been trained on features which are created using the feature pipeline, the same feature pipeline is stored within the model and executed at prediction time.
As during training, intermediate node properties created by the node property steps in the feature pipeline are transient and not visible after execution.
When running the prediction mode, the relationships of the input graph are used in two ways.
First, the input graph is fed into the feature pipeline and therefore influences the predictions if there is at least step in the pipeline which uses the input relationships (typically any node property step does).
Second, the predictions are carried out on each node pair that are not connected via a relationship in the input graph.

The predictions are sorted by score before the ones having score below the configured `threshold` are discarded.
Finally, the configured `topN` predictions are stored back to the in-memory graph.

It is necessary that the predict graph contains the properties that the pipeline requires and that the used array properties have the same dimensions as in the train graph.
If the predict and train graphs are distinct, it is also beneficial that they have similar origins and semantics, so that the model is able to generalize well.

=== Syntax

.Link Prediction syntax per mode
[.tabbed-example, caption = ]
====
[.include-with-mutate]
======
.Run Link Prediction in mutate mode on a named graph:
[source, cypher, role=noplay]
----
CALL gds.alpha.ml.pipeline.linkPrediction.predict.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  createMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  mutateMillis: Integer,
  relationshipsWritten: Integer,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

.Configuration
[opts="header",cols="1,1,2m,1,5"]
|===
| Name                                                          | Type              | Default           | Optional | Description
| modelName                                                     | String            | n/a               | no       | The name of a Link Prediction model in the model catalog.
| <<common-configuration-node-labels,nodeLabels>>               | List of String    | ['*']             | yes      | Filter the named graph using the given node labels.
| <<common-configuration-relationship-types,relationshipTypes>> | List of String    | ['*']             | yes      | Filter the named graph using the given relationship types.
| <<common-configuration-concurrency,concurrency>>              | Integer           | 4                 | yes      | The number of concurrent threads used for running the algorithm.
| mutateRelationshipType                                        | String            | n/a               | no       | The relationship type used for the new relationships written to the in-memory graph.
| mutateProperty                                                | String            | 'probability'     | yes      | The relationship property in the GDS graph to which the result is written.
|===

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name      | Type    | Default | Optional | Description
| topN      | Integer | n/a     | no       | Limit on predicted relationships to output.
| threshold | Float   | 0.0     | yes       | Minimum predicted probability on relationships to output.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                 | Type    | Description
| createMillis         | Integer | Milliseconds for creating the graph.
| computeMillis        | Integer | Milliseconds for running the algorithm.
| postProcessingMillis | Integer | Milliseconds for computing the global metrics.
| mutateMillis         | Integer | Milliseconds for adding properties to the in-memory graph.
| relationshipsWritten | Integer | Number of relationships created.
| configuration        | Map     | Configuration used for running the algorithm.
|===
======
====

=== Example

In this example we will show how to use a trained model to predict new relationships in your in-memory graph.
In order to do this, we must first have an already trained model registered in the Model Catalog.
We will use the model which we trained in the <<algorithms-ml-linkprediction-pipeline-examples-train-query, train example>> which we gave the name `lp-pipeline-model`.
The algorithm excludes predictions for existing relationships in the graph.


[role=query-example, group=lp]
--
[source, cypher]
----
CALL gds.alpha.ml.pipeline.linkPrediction.predict.mutate('myGraph', {
  modelName: 'lp-pipeline-model',
  mutateRelationshipType: 'KNOWS_PREDICTED',
  topN: 5,
  threshold: 0.45
}) YIELD relationshipsWritten
----

.Results
[opts="header"]
|===
| relationshipsWritten
| 10
|===
--

We specified `threshold` to filter out predictions with probability less than 45%, and `topN` to further limit output to the top 5 relationships.
Because we are using the `UNDIRECTED` orientation, we will write twice as many relationships to the in-memory graph.

In the following, we will inspect the predicted relationships:

[role=query-example, group=lp]
--
.Stream the predicted relationships:
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperty('myGraph', 'probability', ['KNOWS_PREDICTED'])
YIELD
  sourceNodeId, targetNodeId, propertyValue
WHERE sourceNodeId < targetNodeId
RETURN
  gds.util.asNode(sourceNodeId).name as source, gds.util.asNode(targetNodeId).name as target, propertyValue AS probability
ORDER BY source ASC, target ASC
----

.Results
[opts="header"]
|===
| source  | target      | probability
| "Alice"   | "Chris"   | 0.5416134109649615
| "Alice"   | "Greg"    | 0.5114214657668055
| "Alice"   | "Karin"   | 0.5116783521370275
| "Alice"   | "Mark"    | 0.5123752811873264
| "Chris"   | "Mark"    | 0.5358186088801173
|===

We can see, that our model predicts the most likely link is between Alice and Chris.
--
