[[alpha-algorithms-rwr]]
[.alpha]
= Random walk with restarts sampling
:description: This section describes the Random walk with restarts sampling algorithm in the Neo4j Graph Data Science library.
:page-aliases: management-ops/projections/rwr.adoc

include::partial$/operations-reference/alpha-note.adoc[]

:directed:
:undirected:
:heterogeneous-nodes:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]

{nbsp} +
[TIP]
====
Random walk with restarts sampling is featured in the end-to-end example Jupyter notebooks:

* https://neo4j.com/docs/graph-data-science-client/current/tutorials/import-sample-export-gnn/[Sampling, Export and Integration with PyG example]
* https://neo4j.com/docs/graph-data-science-client/current/tutorials/node-regression-with-subgraph-and-graph-sample/[Node Regression with Subgraph and Graph Sample projections]
====

== Introduction

Sometimes it may be useful to have a smaller but structurally representative sample of a given graph.
For instance, such a sample could be used to train an inductive embedding algorithm (such as a graph neural network, like xref:machine-learning/node-embeddings/graph-sage.adoc[GraphSAGE]). The training would then be faster than when training on the entire graph, and then the trained model could still be used to predict embeddings on the entire graph.

Random walk with restarts (RWR) samples the graph by taking random walks from a set of start nodes (see the xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`startNodes`] parameter below).
On each step of a random walk, there is some probability (see the xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`restartProbability`] parameter below) that the walk stops, and a new walk from one of the start nodes starts instead (i.e. the walk restarts).
Each node visited on these walks will be part of the sampled subgraph.
The algorithm stops walking when the requested number of nodes have been visited (see the xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`samplingRatio`] parameter below).
The relationships of the sampled subgraph are those induced by the sampled nodes (i.e. the relationships of the original graph that connect nodes that have been sampled).

If at some point it's very unlikely to visit new nodes by random walking from the current set of start nodes (possibly due to the original graph being disconnected), the algorithm will lazily expand the pool of start nodes one at a time by picking nodes uniformly at random from the original graph.

It was shown by Leskovec et al. in the paper _"Sampling from Large Graphs"_ that RWR is a very good sampling algorithm for preserving structural features of the original graph that was sampled from. Additionally, RWR has been successfully used throughout the literature to sample batches for graph neural network (GNN) training.

[Note]
====
Random walk with restarts is sometimes also referred to as _rooted_ or _personalized_ random walk.
====


=== Relationship weights

If the graph is weighted and xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`relationshipWeightProperty`]  is specified, the random walks are weighted.
This means that the probability of walking along a relationship is the weight of that relationship divided by the sum of weights of outgoing relationships from the current node.


=== Node label stratification

In some cases it may be desirable for the sampled graph to preserve the distribution of node labels of the original graph.
To enable such stratification, one can set xref:management-ops/graph-creation/sampling/rwr.adoc#alpha-algorithms-rwr-syntax-configuration[`nodeLabelStratification`] to `true` in the algorithm configuration.
The stratified sampling is performed by only adding a node to the sampled graph if more nodes of that node's particular set of labels are needed to uphold the node label distribution of the original graph.

By default, the algorithm treats all nodes in the same way no matter how they are labeled and makes no special effort to preserve the node label distribution of the original graph.
Please note that the stratified sampling might be a bit slower since it has restrictions on the types of nodes it can add to the sampled graph when crawling it.

[Note]
====
At this time there is no support for relationship type stratification.
====


== Syntax

[.project-sample-syntax]
--
.The following describes the API for running the algorithm
[source, cypher, role=noplay]
----
CALL gds.graph.sample.rwr(
  graphName: String,
  fromGraphName: String,
  configuration: Map
)
YIELD
  graphName,
  fromGraphName,
  nodeCount,
  relationshipCount,
  startNodeCount,
  projectMillis
----

.Parameters
[opts="header",cols="1,1,4"]
|===
| Name                | Type    | Description
| graphName           | String  | The name of the new graph that is stored in the graph catalog.
| fromGraphName       | String  | The name of the original graph in the graph catalog.
| configuration       | Map     | Additional parameters to configure the subgraph sampling.
|===

[[alpha-algorithms-rwr-syntax-configuration]]
.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name                    | Type            | Default                           | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null    | yes       | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
| samplingRatio           | Float           | 0.15                              | yes      | The fraction of nodes in the original graph to be sampled.
| restartProbability      | Float           | 0.1                               | yes      | The probability that a sampling random walk restarts from one of the start nodes.
| startNodes              | List of Integer | A node chosen uniformly at random | yes      | IDs of the initial set of nodes of the original graph from which the sampling random walks will start.
| nodeLabelStratification | Boolean         | false                             | yes      | If true, preserves the node label distribution of the original graph.
| randomSeed              | Integer         | n/a                               | yes      | A random seed which is used for all randomness in the computation. Requires `concurrency = 1`.
|===

.Results
[opts="header",cols="1,1,4"]
|===
| Name                   | Type     | Description
| graphName              | String   | The name of the new graph that is stored in the graph catalog.
| fromGraphName          | String   | The name of the original graph in the graph catalog.
| nodeCount              | Integer  | Number of nodes in the subgraph.
| relationshipCount      | Integer  | Number of relationships in the subgraph.
| startNodeCount         | Integer  | Number of start nodes actually used by the algorithm.
| projectMillis          | Integer  | Milliseconds for projecting the subgraph.
|===
--


== Examples

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

In this section we will demonstrate the usage of the RWR sampling algorithm on a small toy graph.


=== Setting up

:algorithm-name: Random walk with restarts sampling
:graph-description: social network
:image-file: two-user-communities.svg
include::partial$/algorithms/shared/examples-intro.adoc[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query, group=example]
----
CREATE
  (nAlice:User {name: 'Alice'}),
  (nBridget:User {name: 'Bridget'}),
  (nCharles:User {name: 'Charles'}),
  (nDoug:User {name: 'Doug'}),
  (nMark:User {name: 'Mark'}),
  (nMichael:User {name: 'Michael'}),

  (nAlice)-[:LINK]->(nBridget),
  (nAlice)-[:LINK]->(nCharles),
  (nCharles)-[:LINK]->(nBridget),

  (nAlice)-[:LINK]->(nDoug),

  (nMark)-[:LINK]->(nDoug),
  (nMark)-[:LINK]->(nMichael),
  (nMichael)-[:LINK]->(nMark);
----

This graph has two clusters of _Users_, that are closely connected.
Between those clusters there is one single relationship.

We can now project the graph and store it in the graph catalog.

.The following statement will project the graph and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query, group=example]
----
CALL gds.graph.project( 'myGraph', 'User', 'LINK' )
----


=== Sampling

We can now go on to sample a subgraph from "myGraph" using RWR.
Using the "Alice" `User` node as our set of start nodes, we will venture to visit four nodes in the graph for our sample.
Since we have six nodes total in our graph, and 4/6 â‰ˆ 0.66 we will use this as our sampling ratio.

[role=query-example, group=example]
--
.The following will run the Random walk with restarts sampling algorithm:
[source, cypher, role=noplay]
----
MATCH (start:User {name: 'Alice'})
CALL gds.graph.sample.rwr('mySample', 'myGraph', { samplingRatio: 0.66, startNodes: [start] })
YIELD nodeCount, relationshipCount
RETURN nodeCount, relationshipCount
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount
| 4         | 4
|===
--

As we can see we did indeed visit four nodes.
Looking at the topology of our original graph, "myGraph", we can conclude that the nodes must be those corresponding to the `User` nodes with the name properties "Alice", "Bridget", "Charles" and "Doug".
And the relationships sampled are those connecting these nodes.

== Disambiguate between sampled parallel relationships

When the graph contains parallel relationships between two nodes, the algorithm will sample any of them at random.
In order to disambiguate between them, the relationship id needs to be added as a property to the projection graph.

In this example graph, we will only have two nodes, but many different relationships between them.
The relationships each have a different property value, which makes them different from one another.

.The following Cypher statement will create an example graph with parallel relationships in the Neo4j database:
[source, cypher, role=noplay setup-query, group=parallel]
----
CREATE
  (nFlorentin:ParUser {name: 'Florentin'}),
  (nPaul:ParUser {name: 'Paul'}),

  (nFlorentin)-[:PAR_LINK { prop: 0.1 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.2 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.3 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.4 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.5 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.6 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.7 }]->(nPaul),
  (nFlorentin)-[:PAR_LINK { prop: 0.8 }]->(nPaul);
----

We can now use a Cypher projection to project the graph with the relationship id as a relationship property and store it in the graph catalog.

.The following statement will project the graph with the relationship id as a property and store it in the graph catalog.
[source, cypher, role=noplay graph-project-query, group=parallel]
----
MATCH (source: ParUser)-[r:PAR_LINK]->(target: ParUser)
RETURN gds.graph.project(
    'parallelGraph', source, target,
    {
         sourceNodeLabels: labels(source),
         targetNodeLabels: labels(target),
         relationshipType: type(r),
         relationshipProperties: { key: id(r) }  //  <1>
    }
)
----
<1> The relationship id is added as a property to the projection graph.

=== Sampling

We can now go on to sample a subgraph from "parallelGraph" using RWR.
Using the "Florentin" `ParUser` node as our set of start nodes, this time we want to have two nodes in our sample.
Since we have eight nodes total in our graph, we use 0.25 as our sampling ratio.

[role=query-example, group=parallel]
--
.The following will run the Random walk with restarts sampling algorithm on the parallel graph:
[source, cypher, role=noplay]
----
MATCH (start:ParUser {name: 'Florentin'})
CALL gds.graph.sample.rwr('parallelSample', 'parallelGraph', { samplingRatio: 1.0, startNodes: [start] })
YIELD nodeCount, relationshipCount
RETURN nodeCount, relationshipCount
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount
| 2         | 8
|===
--

We can now inspect the sampled relationships and disambiguate them by their `key` property, which is the original relationship id.

[role=query-example, group=parallel]
--
.The following will stream the relationship id property from the sampled parallel graph:
[source, cypher, role=noplay]
----
CALL gds.graph.relationshipProperty.stream('parallelSample', 'key', ['PAR_LINK'])
YIELD sourceNodeId, targetNodeId, propertyValue AS relationshipId
MATCH (n)-[r]->(m)
WHERE id(r) = relationshipId
RETURN n.name AS source, m.name AS target, r.prop AS prop
ORDER BY prop
----

.Results
[opts="header"]
|===
| source       | target | prop
| "Florentin"  | "Paul" | 0.1
| "Florentin"  | "Paul" | 0.2
| "Florentin"  | "Paul" | 0.3
| "Florentin"  | "Paul" | 0.4
| "Florentin"  | "Paul" | 0.5
| "Florentin"  | "Paul" | 0.6
| "Florentin"  | "Paul" | 0.7
| "Florentin"  | "Paul" | 0.8
|===
--
