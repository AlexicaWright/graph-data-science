[[getting-started-ml-pipeline]]
= Machine learning pipeline
:description: This chapter shows a complete example using machine learning pipelines from the Neo4j Graph Data Science library.
:keywords: GDS, getting started, machine learning, pipeline
:sectnums!:

A GDS pipeline is:

. Configured
. Trained
. Used for prediction

Currently, the following types of pipelines are available:

* Node Classification
* Link Prediction
* Node Regression

[NOTE]
====
This example is a simplified version of the Link Prediction pipeline described in the xref:machine-learning/linkprediction-pipelines/config.adoc[configuration] and xref:machine-learning/linkprediction-pipelines/training.adoc[training] sections of the manual.
As such, the model performance is not expected to be the best.
====

== Setup

----
CREATE
  (alice:Person {name: 'Alice', age: 38}),
  (michael:Person {name: 'Michael', age: 67}),
  (karin:Person {name: 'Karin', age: 30}),
  (chris:Person {name: 'Chris', age: 52}),
  (will:Person {name: 'Will', age: 6}),
  (mark:Person {name: 'Mark', age: 32}),
  (greg:Person {name: 'Greg', age: 29}),
  (veselin:Person {name: 'Veselin', age: 3}),

  (alice)-[:KNOWS]->(michael),
  (michael)-[:KNOWS]->(karin),
  (michael)-[:KNOWS]->(chris),
  (michael)-[:KNOWS]->(greg),
  (will)-[:KNOWS]->(michael),
  (will)-[:KNOWS]->(chris),
  (mark)-[:KNOWS]->(michael),
  (mark)-[:KNOWS]->(will),
  (greg)-[:KNOWS]->(chris),
  (veselin)-[:KNOWS]->(chris),
  (karin)-[:KNOWS]->(veselin),
  (chris)-[:KNOWS]->(karin)
----

image::lp-graph.svg["LP example data."]

NOTE: The Link Prediction model requires the graph to be created using the UNDIRECTED orientation for relationships.

----
CALL gds.graph.project(
  'myGraph',
  {
    Person: {
      properties: ['age']
    }
  },
  {
    KNOWS: {
      orientation: 'UNDIRECTED'
    }
  }
)
----

== Configuration

Steps to configuration:

. Create the pipeline.
. Add features.
. Define the split.
. Add the candidate models.

Whole block without returning values (no YIELD, no RETURN):

----
CALL gds.beta.pipeline.linkPrediction.create('pipe');

CALL gds.beta.pipeline.linkPrediction.addFeature(
  'pipe', 
  'cosine',
  {
    nodeProperties: ['age']
  }
);

CALL gds.beta.pipeline.linkPrediction.configureSplit(
  'pipe',
  {
    testFraction: 0.25,
    trainFraction: 0.6,
    validationFolds: 3
  }
);

CALL gds.beta.pipeline.linkPrediction.addLogisticRegression('pipe');
----

Optionally, two more steps:

* Add node property
* Autotuning

== Train

The configured pipeline is ready for training.

----
CALL gds.beta.pipeline.linkPrediction.train(
  'myGraph',
  {
    pipeline: 'pipe',
    modelName: 'lp-pipeline-model',
    metrics: ['AUCPR'],
    targetRelationshipType: 'KNOWS'
  }
)
YIELD modelInfo, modelSelectionStats
RETURN
  modelInfo.bestParameters AS winningModel,
  modelInfo.metrics.AUCPR.train.avg AS avgTrainScore,
  modelInfo.metrics.AUCPR.validation.avg AS avgValidationScore,
  modelInfo.metrics.AUCPR.outerTrain AS outerTrainScore,
  modelInfo.metrics.AUCPR.test AS testScore
----

Various metrics are reported.

== Predict

Predict the probability that a link exists between two nodes.

----
CALL gds.beta.pipeline.linkPrediction.predict.stream(
  'myGraph',
  {
    modelName: 'lp-pipeline-model',
    topN: 5
  }
)
YIELD node1, node2, probability
RETURN
  gds.util.asNode(node1).name AS person1,
  gds.util.asNode(node2).name AS person2,
  probability
ORDER BY probability DESC, person1
----