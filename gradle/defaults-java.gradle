allprojects {
    plugins.withType(JavaPlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }
    }
}

allprojects {
    plugins.withType(JavaLibraryPlugin) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        repositories {
            mavenCentral()
            if (project.hasProperty('pipelineRepository')) {
                maven {
                    url = "file://${project.getProperty('pipelineRepository')}"
                }
            }
            maven {
                name = 'teamcity-artifacts'
                url = "file://${rootProject.projectDir}/neo4j-repo"
            }
            mavenLocal()
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from(sourceSets.main.allJava)
        }

        javadoc {
            StandardJavadocDocletOptions opts = (options as StandardJavadocDocletOptions)
            opts.addBooleanOption('Xdoclint:all,-missing', true)
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        test {
            debugOptions {
                enabled = project.hasProperty('test-debug')
                port = 5425
                server = true
                suspend = true
            }

            // Since JDK 17 the default access to the following
            // packages is "deny". We need to open them in order
            // to gain access in GDS / Neo4j.
            jvmArgs += '--add-opens=java.base/java.lang=ALL-UNNAMED'
            jvmArgs += '--add-opens=java.base/java.lang.invoke=ALL-UNNAMED'
            jvmArgs += '--add-opens=java.base/java.nio=ALL-UNNAMED'
            jvmArgs += '--add-opens=java.base/java.util.stream=ALL-UNNAMED'
            jvmArgs += '--add-opens=java.base/sun.net.www.protocol.http=ALL-UNNAMED'
            jvmArgs += '--add-opens=java.base/com.sun.tools.javac.api=ALL-UNNAMED'

            maxHeapSize = "3G"
            useJUnitPlatform {
                includeEngines 'junit-jupiter'
            }
            // enable verbose test logging
            // testLogging {
            //     events "passed", "skipped", "failed", "standardOut", "standardError"
            // }
            testLogging {
                afterSuite { desc, result ->
                    if (!desc.parent) { // will match the outermost suite
                        println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    }
                }
            }
        }

        tasks.withType(JavaCompile) {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
            options.warnings = true
            options.encoding = 'UTF-8'
            options.forkOptions.jvmArgs += '--illegal-access=permit'
            options.compilerArgs += [
                    '-parameters',
                    '-Xlint:all,-cast,-deprecation,-processing,-serial,-try,-unchecked,-varargs,-rawtypes,-removal',
                    '-Xmaxerrs', '1337'
            ]
        }

        afterEvaluate {
            if (group.startsWith('com.neo4j.gds')) {
                javadoc {
                    enabled false
                }
            }
        }
    }
}
