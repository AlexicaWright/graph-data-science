apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

description = 'Neo4j Graph Data Science :: Serialization'

protobuf {
    // Configure the protoc executable
    protoc {
        if (project.hasProperty("localProtoc")) {
            // use local installation of protoc
            path = project.property("localProtoc");
        } else {
            // Download from repositories
            artifact = "com.google.protobuf:protoc:${ver.'protobuf'}"
        }
        generatedFilesBaseDir = "$buildDir/generated-protos"
    }
    generateProtoTasks {
        all().each { protoTask ->
            protoTask.outputs.cacheIf { false }
        }
    }
}

dependencies {
    annotationProcessor project(':annotations')
    annotationProcessor project(':config-generator')
    annotationProcessor group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    annotationProcessor group: 'org.immutables',           name: 'value',              version: ver.'immutables'
    annotationProcessor group: 'org.neo4j',                name: 'annotations',        version: ver.'neo4j'

    implementation      group: 'com.google.protobuf',        name: 'protobuf-java',    version: ver.'protobuf'
    implementation      group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: ver.'spotbugsToolVersion'
    implementation      group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.6'

    compileOnly         group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    compileOnly         group: 'org.immutables',           name: 'value-annotations',  version: ver.'immutables'
    compileOnly         group: 'org.jetbrains',            name: 'annotations',        version: ver.'jetbrains-annotations'
    neodeps.each {
        compileOnly(group: 'org.neo4j', name: it, version: ver.'neo4j') {
            transitive = false
        }
    }

    implementation group: 'com.esotericsoftware', name: 'kryo', version: ver.'kryo'

    implementation project(':core')
    implementation project(':ml-core')
    implementation project(':algo')
    implementation project(':alpha-algo')
    testCompile project(':alpha-algo').sourceSets.test.output

    testImplementation project(':test-utils')
    testCompileOnly group: 'org.immutables',      name: 'value-annotations',    version: ver.'immutables'
    testCompileOnly group: 'org.immutables',      name: 'builder',              version: ver.'immutables'
    testCompileOnly group: 'org.jetbrains',       name: 'annotations',          version: ver.'jetbrains-annotations'
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: ver.'spotbugsToolVersion'

    neodeps.each {
        testCompileOnly(group: 'org.neo4j', name: it, version: ver.'neo4j') {
            transitive = false
        }
    }

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.6'

    testAnnotationProcessor project(':annotations')
    testAnnotationProcessor project(':config-generator')
    testAnnotationProcessor group: 'org.immutables',           name: 'value',              version: ver.'immutables'
    testAnnotationProcessor group: 'org.neo4j',                name: 'annotations',        version: ver.'neo4j'
}
