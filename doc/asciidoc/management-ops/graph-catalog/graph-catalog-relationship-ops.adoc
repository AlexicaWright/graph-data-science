[[graph-catalog-relationship-ops]]
= Relationship operations

[abstract]
--
This section details the operations available over relationships and relationship properties stored in projected graphs within the Neo4j Graph Data Science library.
--

The graphs in the Neo4j Graph Data Science Library support properties for relationships.
We provide multiple operations to work with the stored relationship-properties in projected graphs.
Relationship properties are either created during the graph creation or when using the `mutate` mode of our graph algorithms.

To inspect stored relationship property values, the `streamRelationshipProperties` procedure can be used.
This is useful if we ran multiple algorithms in `mutate` mode and want to retrieve some or all of the results.

To persist relationship types in a Neo4j database, we can use `gds.graph.writeRelationship`.
Similar to streaming relationship properties, it is also possible to write back to Neo4j.
This is similar to what an algorithm `write` execution mode does, but allows more fine-grained control over the operations.
By default, no relationship properties will be written. To write relationship properties, these have to be explicitly specified.

We can also remove relationships from a named graph in the catalog.
This is useful to free up main memory or to remove accidentally created relationship types.

== Syntax

.Syntax descriptions of the different operations over relationship types
[.tabbed-example, caption=]
====

[.include-with-stream-single-property]
======
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperty(
    graphName: String,
    relationshipProperties: List of String,
    relationshipTypes: List of Strings,
    configuration: Map
)
YIELD
    sourceNodeId: Integer,
    targetNodeId: Integer,
    relationshipType: String,
    propertyValue: Integer or Float
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name                   | Type                       | Optional | Description
| graphName              | String                     | no       | The name under which the graph is stored in the catalog.
| relationshipProperties | List of String             | no       | The relationship properties in the graph to stream.
| relationshipTypes      | List of Strings            | yes      | The relationship types to stream the relationship properties for graph.
| configuration          | Map                        | yes      | Additional parameters to configure streamNodeProperties.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default | Description
| concurrency            | Integer               | 4       | The number of concurrent threads. Note, this procedure is always running single-threaded.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name                  | Type                                                 | Description
| sourceNodeId          | Integer                                              | The id of the source node for the relationship.
| targetNodeId          | Integer                                              | The id of the target node for the relationship.
| relationshipType      | Integer                                              | The type of the relationship.
.^| propertyValue
a|
* Integer
* Float
.^| The stored property value.
|===
======

[.include-with-stream]
======
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperties(
    graphName: String,
    relationshipProperties: List of String,
    relationshipTypes: List of Strings,
    configuration: Map
)
YIELD
    sourceNodeId: Integer,
    targetNodeId: Integer,
    relationshipType: String,
    relationshipProperty: String,
    propertyValue: Integer or Float
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name                   | Type                       | Optional | Description
| graphName              | String                     | no       | The name under which the graph is stored in the catalog.
| relationshipProperties | List of String             | no       | The relationship properties in the graph to stream.
| relationshipTypes      | List of Strings            | yes      | The relationship types to stream the relationship properties for graph.
| configuration          | Map                        | yes      | Additional parameters to configure streamNodeProperties.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default | Description
| concurrency            | Integer               | 4       | The number of concurrent threads. Note, this procedure is always running single-threaded.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name                  | Type                                                 | Description
| sourceNodeId          | Integer                                              | The id of the source node for the relationship.
| targetNodeId          | Integer                                              | The id of the target node for the relationship.
| relationshipType      | Integer                                              | The type of the relationship.
| relationshipProperty  | Integer                                              | The name of the relationship property.
.^| propertyValue
a|
* Integer
* Float
.^| The stored property value.
|===
======

[.include-with-write]
======
[source, cypher, role=noplay]
----
CALL gds.graph.writeRelationship(
    graphName: String,
    relationshipType: String,
    relationshipProperty: String,
    configuration: Map
)
YIELD
  writeMillis: Integer,
  graphName: String,
  relationshipType: String,
  relationshipsWritten: Integer,
  relationshipProperty: String,
  propertiesWritten: Integer
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name                  | Type    | Optional | Description
| graphName             | String  | no       | The name under which the graph is stored in the catalog.
| relationshipType      | String  | no       | The relationship type in the graph to write back.
| relationshipProperty  | String  | yes      | The relationship property to write back.
| configuration         | Map     | yes      | Additional parameters to configure writeRelationship.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default           | Description
| concurrency            | Integer               | 4                 | The number of concurrent threads used for running the procedure. Also provides the default value for `writeConcurrency`
| writeConcurrency       | Integer               | 'concurrency'     | The number of concurrent threads used for writing the node properties.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name                  | Type                     | Description
| writeMillis           | Integer                  | Milliseconds for writing result data back to Neo4j.
| graphName             | String                   | The name of a graph stored in the catalog.
| relationshipType      | String                   | The type of the relationship that was written.
| relationshipsWritten  | Integer                  | Number relationships written.
| relationshipProperty  | String                   | The name of the relationship property that was written.
| propertiesWritten     | Integer                  | Number relationships properties written.
|===
======

[.include-with-remove]
======
[source, cypher, role=noplay]
----
CALL gds.graph.deleteRelationships(
    graphName: String,
    relationshipType: String
)
YIELD
  graphName: String,
  relationshipType: String,
  deletedRelationships: Integer,
  deletedProperties: Map
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name             | Type    | Optional | Description
| graphName        | String  | no       | The name under which the graph is stored in the catalog.
| relationshipType | String  | no       | The relationship type in the graph to remove.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name                 | Type       | Description
| graphName            | String     | The name of a graph stored in the catalog.
| relationshipType     | String     | The type of the removed relationships.
| deletedRelationships | Integer    | Number of removed relationships from the in-memory graph.
| deletedProperties    | Integer    | Map where the key is the name of the relationship property, and the value is the number of removed properties under that name.
|===
======
====


== Examples

In order to demonstrate the GDS capabilities over node properties, we are going to create a small graph in Neo4j and project it into our graph catalog.

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (alice:Person {name: 'Alice'}),
  (bob:Person {name: 'Bob'}),
  (carol:Person {name: 'Carol'}),
  (dave:Person {name: 'Dave'}),
  (eve:Person {name: 'Eve'}),
  (guitar:Instrument {name: 'Guitar'}),
  (synth:Instrument {name: 'Synthesizer'}),
  (bongos:Instrument {name: 'Bongos'}),
  (trumpet:Instrument {name: 'Trumpet'}),

  (alice)-[:LIKES { score: 5 }]->(guitar),
  (alice)-[:LIKES { score: 4 }]->(synth),
  (alice)-[:LIKES { score: 3, strength: 0.5}]->(bongos),
  (bob)-[:LIKES { score: 4 }]->(guitar),
  (bob)-[:LIKES { score: 5 }]->(synth),
  (carol)-[:LIKES { score: 2 }]->(bongos),
  (dave)-[:LIKES { score: 3 }]->(guitar),
  (dave)-[:LIKES { score: 1 }]->(synth),
  (dave)-[:LIKES { score: 5 }]->(bongos);
----

.Project the graph:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'personsAndInstruments',
  ['Person', 'Instrument'],
  {
    LIKES: {
      type: 'LIKES',
      properties: {
        strength: {
          property: 'strength',
          defaultValue: 1.0
        },
        score: {
          property: 'score'
        }
      }
    }
  }
);
----

.Compute the Node Similarity in our graph:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.nodeSimilarity.mutate('personsAndInstruments', {
  mutateRelationshipType: 'SIMILAR',
  mutateProperty: 'score'
})
----


=== Stream

==== Single property

[role=query-example]
--
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperty('personsAndInstruments', 'score')
YIELD
  sourceNodeId, targetNodeId, relationshipType, propertyValue
RETURN
  sourceNodeId, targetNodeId, relationshipType, propertyValue
ORDER BY sourceNodeId ASC, targetNodeId ASC
----

.Results
[opts="header"]
|===
| sourceNodeId  | targetNodeId  | relationshipType  | propertyValue
| 0             | 1             | "SIMILAR"         | 0.6666666666666666
| 0             | 2             | "SIMILAR"         | 0.3333333333333333
| 0             | 3             | "SIMILAR"         | 1.0
| 0             | 5             | "LIKES"           | 5.0
| 0             | 6             | "LIKES"           | 4.0
| 0             | 7             | "LIKES"           | 3.0
| 1             | 0             | "SIMILAR"         | 0.6666666666666666
| 1             | 3             | "SIMILAR"         | 0.6666666666666666
| 1             | 5             | "LIKES"           | 4.0
| 1             | 6             | "LIKES"           | 5.0
| 2             | 0             | "SIMILAR"         | 0.3333333333333333
| 2             | 3             | "SIMILAR"         | 0.3333333333333333
| 2             | 7             | "LIKES"           | 2.0
| 3             | 0             | "SIMILAR"         | 1.0
| 3             | 1             | "SIMILAR"         | 0.6666666666666666
| 3             | 2             | "SIMILAR"         | 0.3333333333333333
| 3             | 5             | "LIKES"           | 3.0
| 3             | 6             | "LIKES"           | 1.0
| 3             | 7             | "LIKES"           | 5.0
|===
--

[role=query-example]
--
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperty('personsAndInstruments', 'score', ['SIMILAR'])
YIELD
  sourceNodeId, targetNodeId, relationshipType, propertyValue
RETURN
  sourceNodeId, targetNodeId, relationshipType, propertyValue
ORDER BY sourceNodeId ASC, targetNodeId ASC
----

.Results
[opts="header"]
|===
| sourceNodeId | targetNodeId |	relationshipType | propertyValue
| 0            | 1            |	"SIMILAR"        | 0.6666666666666666
| 0            | 2            |	"SIMILAR"        | 0.3333333333333333
| 0            | 3            |	"SIMILAR"        | 1.0
| 1            | 0            |	"SIMILAR"        | 0.6666666666666666
| 1            | 3            |	"SIMILAR"        | 0.6666666666666666
| 2            | 0            |	"SIMILAR"        | 0.3333333333333333
| 2            | 3            |	"SIMILAR"        | 0.3333333333333333
| 3            | 0            |	"SIMILAR"        | 1.0
| 3            | 1            |	"SIMILAR"        | 0.6666666666666666
| 3            | 2            |	"SIMILAR"        | 0.3333333333333333
|===
--

==== Multiple properties


[role=query-example]
--
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperties('personsAndInstruments', ['score', 'strength'], ['LIKES'])
YIELD
  sourceNodeId, targetNodeId, relationshipType, relationshipProperty, propertyValue
RETURN
  sourceNodeId, targetNodeId, relationshipType, relationshipProperty, propertyValue
ORDER BY sourceNodeId ASC, targetNodeId ASC
----

.Results
[opts="header"]
|===
| sourceNodeId | targetNodeId |	relationshipType |	relationshipProperty |	propertyValue
| 0            | 5            |	"LIKES"	         | "score"               |	5.0
| 0            | 5            |	"LIKES"	         | "strength"            |	1.0
| 0            | 6            |	"LIKES"	         | "score"               |	4.0
| 0            | 6            |	"LIKES"	         | "strength"            |	1.0
| 0            | 7            |	"LIKES"	         | "score"               |	3.0
| 0            | 7            |	"LIKES"	         | "strength"            |	0.5
| 1            | 5            |	"LIKES"	         | "score"               |	4.0
| 1            | 5            |	"LIKES"	         | "strength"            |	1.0
| 1            | 6            |	"LIKES"	         | "score"               |	5.0
| 1            | 6            |	"LIKES"	         | "strength"            |	1.0
| 2            | 7            |	"LIKES"	         | "score"               |	2.0
| 2            | 7            |	"LIKES"	         | "strength"            |	1.0
| 3            | 5            |	"LIKES"	         | "score"               |	3.0
| 3            | 5            |	"LIKES"	         | "strength"            |	1.0
| 3            | 6            |	"LIKES"	         | "score"               |	1.0
| 3            | 6            |	"LIKES"	         | "strength"            |	1.0
| 3            | 7            |	"LIKES"	         | "score"               |	5.0
| 3            | 7            |	"LIKES"	         | "strength"            |	1.0
|===
--


==== Multiple relationship types

[role=query-example]
--
[source, cypher, role=noplay]
----
CALL gds.graph.streamRelationshipProperties('personsAndInstruments', ['score'], ['LIKES', 'SIMILAR'])
YIELD
  sourceNodeId, targetNodeId, relationshipType, relationshipProperty, propertyValue
RETURN
  sourceNodeId, targetNodeId, relationshipType, relationshipProperty, propertyValue
ORDER BY sourceNodeId ASC, targetNodeId ASC
----

.Results
[opts="header"]
|===
| sourceNodeId  |targetNodeId  | relationshipType  |relationshipProperty  | propertyValue
| 0             | 1            | "SIMILAR"         | "score"              | 0.6666666666666666
| 0             | 2            | "SIMILAR"         | "score"              | 0.3333333333333333
| 0             | 3            | "SIMILAR"         | "score"              | 1.0
| 0             | 5            | "LIKES"           | "score"              | 5.0
| 0             | 6            | "LIKES"           | "score"              | 4.0
| 0             | 7            | "LIKES"           | "score"              | 3.0
| 1             | 0            | "SIMILAR"         | "score"              | 0.6666666666666666
| 1             | 3            | "SIMILAR"         | "score"              | 0.6666666666666666
| 1             | 5            | "LIKES"           | "score"              | 4.0
| 1             | 6            | "LIKES"           | "score"              | 5.0
| 2             | 0            | "SIMILAR"         | "score"              | 0.3333333333333333
| 2             | 3            | "SIMILAR"         | "score"              | 0.3333333333333333
| 2             | 7            | "LIKES"           | "score"              | 2.0
| 3             | 0            | "SIMILAR"         | "score"              | 1.0
| 3             | 1            | "SIMILAR"         | "score"              | 0.6666666666666666
| 3             | 2            | "SIMILAR"         | "score"              | 0.3333333333333333
| 3             | 5            | "LIKES"           | "score"              | 3.0
| 3             | 6            | "LIKES"           | "score"              | 1.0
| 3             | 7            | "LIKES"           | "score"              | 5.0
|===
--

=== Write

=== Remove

include::graph-stream-relationship-properties.adoc[leveloffset=+1]

include::graph-delete-relationship-type.adoc[leveloffset=+1]

include::graph-write-relationship.adoc[leveloffset=+1]
