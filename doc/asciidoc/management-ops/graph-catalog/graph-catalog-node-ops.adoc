[[graph-catalog-node-ops]]
= Node operations

[abstract]
--
This section details the operations available over node-properties stored in projected graphs within the Neo4j Graph Data Science library.
--

The graphs in the Neo4j Graph Data Science Libarary support properties for nodes.
We provide multiple operations to work with the stored node-properties in projected graphs.
Node properties are either created during the graph creation or when using the `mutate` mode of our graph algorithms.

To inspect stored values, the `gds.graph.streamNodeProperties` can be used.
This is useful if we ran multiple algorithms in `mutate` mode and want to retrieve some or all of the results.

To persist the values in a Neo4j database, we can use `gds.graph.writeNodeProperties`.
Similar to streaming node properties, it is also possible to write those back to Neo4j.
This is similar to what the `stream` execution mode does, but allows more fine-grained control over the operations.

We can also remove node properties from a named graph in the catalog.
This is useful to free up main memory or to remove accidentally created node properties.

== Syntax

.Syntax descriptions of the different operations over node properties
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
[source, cypher, role=noplay]
----
CALL gds.graph.streamNodeProperties(
    graphName: String,
    nodeProperties: String or List of Strings,
    nodeLabels: String or List of Strings,
    configuration: Map
)
YIELD
    nodeId: Integer,
    nodeProperty: String,
    propertyValue: Integer or Float or List of Integer or List of Float
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name           | Type                       | Optional | Description
| graphName      | String                     | no       | The name under which the graph is stored in the catalog.
| nodeProperties | String or List of Strings  | no       | The node properties in the graph to stream.
| nodeLabels     | String or List of Strings  | yes      | The node labels to stream the node properties for graph.
| configuration  | Map                        | yes      | Additional parameters to configure streamNodeProperties.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default | Description
| concurrency            | Integer               | 4       | The number of concurrent threads. Note, this procedure is always running single-threaded.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name            | Type                                                 | Description
|nodeId           | Integer                                              | The id of the node.
|nodeProperty     | String                                               | The name of the node property.
.^|propertyValue    a|
* Integer
* Float
* List of Integer
* List of Float  .^| The stored property value.
|===
======

[.include-with-write]
======
[source, cypher, role=noplay]
----
CALL gds.graph.writeNodeProperties(
    graphName: String,
    nodeProperties: String or List of Strings,
    nodeLabels: String or List of Strings,
    configuration: Map
)
YIELD
    writeMillis: Integer,
    propertiesWritten: Integer,
    graphName: String,
    nodeProperties: String or List of String
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name           | Type                       | Optional | Description
| graphName      | String                     | no       | The name under which the graph is stored in the catalog.
| nodeProperties | String or List of Strings  | no       | The node properties in the graph to write back.
| nodeLabels     | String or List of Strings  | yes      | The node labels to write back their node properties.
| configuration  | Map                        | yes      | Additional parameters to configure writeNodeProperties.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default           | Description
| concurrency            | Integer               | 4                 | The number of concurrent threads used for running the procedure. Also provides the default value for `writeConcurrency`
| writeConcurrency       | Integer               | 'concurrency'     | The number of concurrent threads used for writing the node properties.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name              | Type                     | Description
| writeMillis       | Integer                  | Milliseconds for writing result data back to Neo4j.
| propertiesWritten | Integer                  | Number of properties written.
| graphName         | String                   | The name of a graph stored in the catalog.
| nodeProperties    | String or List of String | The written node properties.
|===
======

[.include-with-remove]
======
[source, cypher, role=noplay]
----
CALL gds.graph.removeNodeProperties(
    graphName: String,
    nodeProperties: String or List of Strings,
    nodeLabels: String or List of Strings,
    configuration: Map
)
YIELD
    propertiesRemoved: Integer,
    graphName: String,
    nodeProperties: String or List of String
----

.Parameters
[opts="header",cols="1,3,1,5"]
|===
| Name           | Type                       | Optional | Description
| graphName      | String                     | no       | The name under which the graph is stored in the catalog.
| nodeProperties | String or List of Strings  | no       | The node properties in the graph to remove.
| nodeLabels     | String or List of Strings  | yes      | The node labels to remove the node properties from.
| configuration  | Map                        | yes      | Additional parameters to configure removeNodeProperties.
|===

.Configuration
[opts="header",cols="1,1,1,7"]
|===
| Name                   | Type                  | Default           | Description
| concurrency            | Integer               | 4                 | The number of concurrent threads. Note, this procedure is always running single-threaded.
|===

.Results
[opts="header",cols="2,3,5"]
|===
| Name              | Type                     | Description
| propertiesRemoved | Integer                  | Number of properties removed.
| graphName         | String                   | The name of a graph stored in the catalog.
| nodeProperties    | String or List of String | The removed node properties.
|===
======
====

// TODO document gds.graph.streamNodeProperty

== Examples

In order to demonstrate the GDS capabilities over node properties, we are going to create a small social network graph in Neo4j and project it into our graph catalog.

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=noplay setup-query]
----
CREATE
  (florentin:Person { name: 'Florentin', age: 16 }),
  (adam:Person { name: 'Adam', age: 18 }),
  (veselin:Person { name: 'Veselin', age: 20 }),
  (hobbit:Book { name: 'The Hobbit', numberOfPages: 310 }),
  (florentin)-[:KNOWS { since: 2010 }]->(adam),
  (florentin)-[:KNOWS { since: 2018 }]->(veselin),
  (adam)-[:READ]->(hobbit)
----

.Project the small social network graph:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.graph.create(
  'socialGraph',
  {
    Person: {properties: "age"},
    Book: {}
  },
  ['KNOWS', 'READ']
)
----

.Compute the connected components in our social graph using `WCC`:
[source, cypher, role=noplay graph-create-query]
----
CALL gds.degree.mutate('socialGraph', {mutateProperty: 'score'})
----

=== Stream

We can stream node properties stored in a named in-memory graph back to the user.
This is useful if we ran multiple algorithms in `mutate` mode and want to retrieve some or all of the results.
This is similar to what the `stream` execution mode does, but allows more fine-grained control over the operations.

==== Single property

In the following, we stream the previously computed scores `score`.

[role=query-example]
--
.Stream node properties for `Person` nodes:
[source, cypher, role=noplay]
----
CALL gds.graph.streamNodeProperty('socialGraph', 'score')
YIELD nodeId, propertyValue
RETURN gds.util.asNode(nodeId).name AS name, propertyValue AS score
ORDER BY score DESC
----

.Results
[opts="header"]
|===
| name        | score
| "Florentin" | 2.0
| "Adam"      | 1.0
| "Veselin"   | 0.0
| "The Hobbit"   | 0.0
|===
--

NOTE: The above example requires all given properties to be present on at least one node projection, and the properties will be streamed for all such projections.


==== NodeLabels

The procedure can be configured to stream just the properties for specific node labels.

[role=query-example]
--
.Stream node properties for `Person` nodes:
[source, cypher, role=noplay]
----
CALL gds.graph.streamNodeProperty('socialGraph', 'score', ['Person'])
YIELD nodeId, propertyValue
RETURN gds.util.asNode(nodeId).name AS name, propertyValue AS score
ORDER BY score DESC
----

.Results
[opts="header"]
|===
| name        | score
| "Florentin" | 2.0
| "Adam"      | 1.0
| "Veselin"   | 0.0
|===
--

It is required that all specified node labels have the node property.


==== Multiple Properties

We can also stream several properties at once.

[role=query-example]
--
.Stream multiple node properties:
[source, cypher, role=noplay]
----
CALL gds.graph.streamNodeProperties('socialGraph', ['score', 'age'])
YIELD nodeId, nodeProperty, propertyValue
RETURN gds.util.asNode(nodeId).name AS name, nodeProperty, propertyValue
ORDER BY name, nodeProperty
----

.Results
[opts="header"]
|===
| name         | nodeProperty  | propertyValue
| "Adam"       | "age"         | 18
| "Adam"       | "score"       | 1.0
| "Florentin"  | "age"         | 16
| "Florentin"  | "score"       | 2.0
| "Veselin"    | "age"         | 20
| "Veselin"    | "score"       | 0.0
| "The Hobbit" | "age"         | NaN
| "The Hobbit" | "score"       | 0.0
|===
--

[NOTE]
====
When streaming multiple node properties, the name of each property is included in the result.
This adds with some overhead, as each property name must be repeated for each node in the result, but is necessary in order to distinguish properties.
====

=== Write

To write the 'score' property for all node labels in the social graph, we use the following query:

[role=query-example]
--
.Write node properties to Neo4j:
[source, cypher, role=noplay]
----
CALL gds.graph.writeNodeProperties('socialGraph', ['score'])
YIELD propertiesWritten
----

.Results
[opts="header"]
|===
| propertiesWritten
| 4
|===
--

The above example requires the `score` property to be present on _at least one_ projected node label, and the properties will be written for all such labels.

==== NodeLabels

The procedure can be configured to write just the properties for some specific node labels.
In the following example, we will only write back the scores of the `Person` nodes.

[role=query-example]
--
.Write node properties of a specific node projection to Neo4j:
[source, cypher, role=noplay]
----
CALL gds.graph.writeNodeProperties('socialGraph', ['score'], ['Person'])
YIELD propertiesWritten
----

.Results
[opts="header"]
|===
| propertiesWritten
| 3
|===
--

[NOTE]
--
If `nodeLabels` is specified, it is required that _all_ given node labels have _all_ of the given properties.
--


=== Remove

.Remove multiple node properties from a named graph:
[source, cypher, role=noplay]
----
CALL gds.graph.removeNodeProperties('my-graph', ['pageRank', 'communityId'])
----

The above example requires all given properties to be present on at least one node projection, and the properties will be removed from all such projections.

The procedure can be configured to remove just the properties for some specific node projections.
In the following example, we ran an algorithm on a sub-graph and subsequently remove the newly created property.

.Remove node properties of a specific node projection:
[source, cypher, role=noplay]
----
CALL gds.graph.create('my-graph', ['A', 'B'], '*')
CALL gds.wcc.mutate('my-graph', {nodeLabels: ['A'], mutateProperty: 'componentId'})
CALL gds.graph.removeNodeProperties('my-graph', ['componentId'], ['A'])
----

When a list of projections that are not `*` is specified, as in the example above, a different validation and execution is applied;
It is then required that all projections have all of the given properties, and they will be removed from all of the projections.

If any of the given projections is `'*'`, the procedure behaves like in the first example.

